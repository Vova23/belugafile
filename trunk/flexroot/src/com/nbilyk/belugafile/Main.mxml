<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
		 minWidth="400"
		 minHeight="300"
		 creationComplete="creationCompleteHandler();"
		 currentState="{CONNECTING_STATE}" xmlns:belugafile="com.nbilyk.belugafile.*" xmlns:effects="com.nbilyk.effects.*" xmlns:nbilyk="http://www.nbilyk.com/2009">

	<mx:Script>
		<![CDATA[
			import mx.containers.TitleWindow;
			import com.nbilyk.belugafile.vo.Preferences;
			import mx.core.Application;
			import mx.managers.PopUpManager;
			import com.nbilyk.belugafile.events.FileClientEvent;
			import __AS3__.vec.Vector;
			import mx.effects.easing.Sine;
			import com.nbilyk.utils.ByteArrayUtils;
			import mx.events.CloseEvent;
			import flash.utils.setInterval;
			import com.nbilyk.belugafile.events.FileReceiveEvent;
			import com.nbilyk.belugafile.events.FileRequestEvent;
			import mx.events.CollectionEvent;
			import com.nbilyk.belugafile.client.BelugaFileClient;
			import com.nbilyk.stratus.StratusClient;
			import mx.validators.Validator;
			import com.nbilyk.stratus.events.StratusEvent;
			import com.nbilyk.display.MessageNotifier;
			import com.nbilyk.stratus.StratusDao;
			import com.nbilyk.belugafile.vo.FileInfo;
			import mx.controls.Button;
			import mx.collections.ArrayCollection;
			
			include "../../../StratusKey.as";
			
			private static const CONNECTING_STATE:String = "connectingState";
			private static const PEER_STATE:String = "peerState";
			private static const FILES_STATE:String = "filesState";
			private static const ERROR_STATE:String = "errorState";
			
			
			private var files:ArrayCollection = new ArrayCollection(); /* Type File */
			[Bindable] private var fileInfos:ArrayCollection = new ArrayCollection(); /* Type FileInfo */
			
			private var stratusDao:StratusDao;
			private var messageNotifier:MessageNotifier = new MessageNotifier();
			[Bindable] private var hostId:String;
			private var _peerStream:NetStream;
			
			private var preferences:Preferences = new Preferences(true);
			
			private var uploadFileInfo:FileInfo;
			private var uploadFileStream:FileStream;
			
			private var downloadFileInfo:FileInfo;
			private var downloadFile:File;
			private var downloadFileStream:FileStream;
			
			private function creationCompleteHandler():void {
				connectToStratus();
				fileInfos.addEventListener(CollectionEvent.COLLECTION_CHANGE, fileInfosChangeHandler);
			}
			
			//--------------------------------
			// Getters / setters
			//--------------------------------
			
			[Bindable(event="peerStreamChange")]
			public function get peerClient():BelugaFileClient {
				if (!peerStream) return null;
				return BelugaFileClient(peerStream.client);
			}
			
			[Bindable(event="peerStreamChange")]
			public function get peerStream():NetStream {
				return _peerStream;
			}
			public function set peerStream(value:NetStream):void {
				if (value == _peerStream) return;
				_peerStream = value;
				dispatchEvent(new Event("peerStreamChange"));
			}
			
			//--------------------------------
			// Stratus methods
			//--------------------------------
			
			private function connectToStratus():void {
				stratusDao = new StratusDao();
				stratusDao.clientFactory = createStratusClient;
				stratusDao.addEventListener(StratusEvent.CONNECT_SUCCESS, stratusConnectSuccessHandler);
				stratusDao.addEventListener(StratusEvent.CONNECT_FAIL, stratusConnectFailHandler);
				stratusDao.addEventListener(StratusEvent.SUBSCRIBING_SUCCESS, subscribingSuccessHandler);
				stratusDao.addEventListener(StratusEvent.SUBSCRIBING_FAIL, subscribingFailHandler);
				stratusDao.addEventListener(StratusEvent.SUBSCRIBER_CONNECTED, subscriberConnectedHandler);
				stratusDao.addEventListener(StratusEvent.SUBSCRIBER_DISCONNECTED, subscriberDisconnectedHandler);
				
				stratusDao.connect(STRATUS_DEV_KEY);
				stratusDao.connection.maxPeerConnections = 1;
			}
			private function createStratusClient():StratusClient {
				var belugaFileClient:BelugaFileClient = new BelugaFileClient();
				belugaFileClient.addEventListener(FileRequestEvent.FILE_REQUEST, fileRequestHandler);
				belugaFileClient.addEventListener(FileReceiveEvent.DATA_RECEIVED, fileDataReceivedHandler);
				belugaFileClient.addEventListener(FileClientEvent.DOWNLOAD_CANCEL, downloadCancelHandler);
				belugaFileClient.addEventListener(FileClientEvent.UPLOAD_CANCEL, uploadCancelHandler);
				return belugaFileClient;
			}
			private function stratusConnectSuccessHandler(event:StratusEvent):void {
				hostId = stratusDao.connection.nearID;
				currentState = PEER_STATE;
			}
			private function stratusConnectFailHandler(event:StratusEvent):void {
				errorString = "Failed to connect to Stratus. Make sure you are connected to the internet and that your network allows for UDP connections.";
				currentState = ERROR_STATE;
			}
			
			private function subscribingSuccessHandler(event:StratusEvent):void {
				peerStream = event.stream;
				currentState = FILES_STATE;
			}
			private function subscribingFailHandler(event:StratusEvent):void {
				peerStream = null;
				handleError("Could not connect to peer.");
			}
			
			private function connectToPeer():void {
				if (Validator.validateAll([peerIdValidator]).length) return;
				if (hostIdInput.text == hostId) {
					hostIdInput.text = "";
					handleError("Do not enter your own host id, enter your peer's host id.");
					return;
				}
				stratusDao.connectToPeer(hostIdInput.text);
			}
			private function subscriberConnectedHandler(event:StratusEvent):void {
				// Send the new subscriber (your peer) whatever information they need on startup.
				event.stream.send("updateFileInfos", fileInfos); // Send them files we may have selected.
			}
			private function subscriberDisconnectedHandler(event:StratusEvent):void {
				currentState = PEER_STATE;
				peerStream = null;
				cancelUpload();
				cancelDownload();
			}

			private function fileInfosChangeHandler(event:CollectionEvent = null):void {
				stratusDao.sendStream.send("updateFileInfos", fileInfos);
			}
		
			//-------------------------------
			// File selection methods
			//-------------------------------
		
			private function selectFiles():void {
				var file:File;
				if (preferences.lastUploadDirectory) file = new File(preferences.lastUploadDirectory);
				else file = File.desktopDirectory;
				file.addEventListener(FileListEvent.SELECT_MULTIPLE, filesSelectHandler);
				file.browseForOpenMultiple("Select files");
			}
			private function filesSelectHandler(event:FileListEvent):void {
				if (event.files.length) {
					preferences.lastUploadDirectory = File(event.files[0]).parent.nativePath;
					preferences.save();
				}
				for each (var file:File in event.files) {
					files.addItem(file);
					fileInfos.addItem(new FileInfo(file));
				}
			}
			private function removeFileHandler(event:Event):void {
				var row:FileRowView = FileRowView(event.currentTarget);
				var index:int = fileInfos.getItemIndex(row.fileInfo);
				files.removeItemAt(index);
				fileInfos.removeItemAt(index);
			}
			
			//--------------------------
			// Download file methods
			//--------------------------
			
			private function downloadFileHandler(event:Event):void {
				var row:FileRowView = FileRowView(event.currentTarget);
				download(row.fileInfo);
			}
			
			/**
			 * Ask where to save the file.
			 */
			private function download(fileInfo:FileInfo):void {
				if (downloadFileStream) return; // There is already an active download.
				downloadFileInfo = fileInfo;
				
				if (preferences.lastDownloadDirectory) downloadFile = new File(preferences.lastDownloadDirectory).resolvePath(downloadFileInfo.name);
				else downloadFile = File.desktopDirectory.resolvePath(downloadFileInfo.name);
				downloadFile.addEventListener(IOErrorEvent.IO_ERROR, inputFileIoErrorHandler);
				downloadFile.addEventListener(SecurityErrorEvent.SECURITY_ERROR, inputFileSecurityErrorHandler);
				downloadFile.addEventListener(Event.SELECT, fileSaveSelectHandler);
				downloadFile.browseForSave("Save As");
			}
			
			/**
			 * We know where to save the file, ask the peer to send the first segment.
			 * If the file already exists, ask if we want to resume or overwrite.
			 */
			private function fileSaveSelectHandler(event:Event):void {
				preferences.lastDownloadDirectory = downloadFile.parent.nativePath;
				preferences.save();
				if (downloadFile.exists) {
					var titleWindow:TitleWindow = new TitleWindow();
					titleWindow.title = "File already exists.";
					var resumePrompt:ResumePrompt = new ResumePrompt();
					resumePrompt.percentWidth = 100;
					resumePrompt.addEventListener(CloseEvent.CLOSE, resumePromptCloseHandler, false, 0, true);
					titleWindow.addChild(resumePrompt);
					PopUpManager.addPopUp(titleWindow, DisplayObject(Application.application), true);
					PopUpManager.centerPopUp(titleWindow);
				} else {
					beginWriting(0);
				}
			}
			private function resumePromptCloseHandler(event:CloseEvent):void {
				if (event.detail == ResumePrompt.OVERWRITE) {
					beginWriting(0);
				} else if (event.detail == ResumePrompt.RESUME) {
					beginWriting(downloadFile.size);
				} else {
					// Cancel
					downloadFile = null;
				}
				PopUpManager.removePopUp(TitleWindow(ResumePrompt(event.currentTarget).parent));
			}
			private function beginWriting(position:Number):void {
				// Begin writing file
				downloadFileStream = new FileStream();
				try {
					var fileMode:String = (position == 0) ? FileMode.WRITE : FileMode.UPDATE;
					downloadFileStream.open(downloadFile, fileMode);
					stratusDao.sendStream.send("requestFile", downloadFileInfo, position);
				} catch (error:Error) {
					downloadFileStream = null;
					handleError("This file cannot be opened. It may already be in use.");
				}
			}
			
			/**
			 * The Sender has sent file data. Write it to file and then ask for the next segment.
			 */
			private function fileDataReceivedHandler(event:FileReceiveEvent):void {
				if (!downloadFileStream) return;
				downloadFileStream.position = event.offset;
				downloadFileStream.writeBytes(event.inputBuffer);
				event.inputBuffer.clear();
				updateDownloadProgressBar(downloadFileStream.position, downloadFileInfo.size);
				if (event.autoRequestNext) stratusDao.sendStream.send("requestFile", downloadFileInfo, downloadFileStream.position);
			}
			private function inputFileIoErrorHandler(event:IOErrorEvent):void {
				handleError("An error occurred while saving.");
			}
			private function inputFileSecurityErrorHandler(event:SecurityErrorEvent):void {
				handleError("A security error occurred while saving.");
			}
			
			//---------------------------
			// Upload file methods
			//---------------------------
			
			/**
			 * A File has been requested, start sending it over.
			 */
			private function fileRequestHandler(event:FileRequestEvent):void {
				if (!uploadFileStream) {
					var fileToSend:File = getFileByFileInfo(event.fileInfo);
					if (!fileToSend) return;
					
					// Initializing a new file transfer.
					uploadFileStream = new FileStream();
					uploadFileInfo = event.fileInfo;
					uploadFileStream.readAhead = uploadFileInfo.getReadAhead();
					uploadFileStream.openAsync(fileToSend, FileMode.READ);
				} else {
					if (uploadFileInfo.path != event.fileInfo.path) return; // Wrong file.
				}
				
				updateUploadProgressBar(event.offset, event.fileInfo.size);
				
				if (event.offset < event.fileInfo.size) {
					uploadFileStream.position = event.offset;
					transmitOutputBuffer();
				} else {
					// Upload complete.
					cancelUpload();
				}
			}
			private function transmitOutputBuffer():void {
				if (!uploadFileStream) return;
				
				// Wait until the read buffer is full.
				if (uploadFileStream.bytesAvailable < uploadFileInfo.getReadAhead() && uploadFileStream.position + uploadFileStream.bytesAvailable < uploadFileInfo.size) {
					callLater(transmitOutputBuffer);
					return; // Read buffer is not full.
				}
				
				var outputBuffer:ByteArray = new ByteArray();
				uploadFileStream.readBytes(outputBuffer);
				stratusDao.sendStream.send("receiveFile", outputBuffer, uploadFileStream.position - outputBuffer.length);
			}
			
			//--------------------------------
			// Cancel handlers and methods
			//--------------------------------
			
			private function downloadCancelHandler(event:Event):void {
				cancelUpload();
			}
			private function uploadCancelHandler(event:Event):void {
				cancelDownload();
			}
			public function cancelDownload():void {
				trace("cancel download: " + downloadFileStream);
				if (!downloadFileStream) return;
				updateDownloadProgressBar(0, 0);
				stratusDao.sendStream.send("cancelDownload");
				
				downloadFileStream.close();
				downloadFileStream = null;
				downloadFileInfo = null;
				
			}
			public function cancelUpload():void {
				trace("cancel upload: " + uploadFileStream);
				if (!uploadFileStream) return;
				updateUploadProgressBar(0, 0);
				stratusDao.sendStream.send("cancelUpload");
				
				uploadFileInfo = null;
				uploadFileStream.close();
				uploadFileStream = null;
			}
			
			//------------------------
			// Utility methods
			//------------------------
			
			private function updateUploadProgressBar(bytesLoaded:Number, bytesTotal:Number):void {
				var uploadFileRow:FileRowView = getUploadFileRow(uploadFileInfo);
				if (!uploadFileRow) return;
				uploadFileRow.setProgress(bytesLoaded, bytesTotal);
			}
			private function updateDownloadProgressBar(bytesLoaded:Number, bytesTotal:Number):void {
				var downloadFileRow:FileRowView = getDownloadFileRow(downloadFileInfo);
				if (!downloadFileRow) return;
				downloadFileRow.setProgress(bytesLoaded, bytesTotal);
			}
			
			private function getUploadFileRow(fileInfo:FileInfo):FileRowView {
				for each (var child:DisplayObject in yourFilesGrid.getChildren()) {
					if (child is FileRowView) {
						if (FileRowView(child).fileInfo.path == fileInfo.path) return FileRowView(child);
					}
				}
				return null;
			}
			private function getDownloadFileRow(fileInfo:FileInfo):FileRowView {
				for each (var child:DisplayObject in theirFilesGrid.getChildren()) {
					if (child is FileRowView) {
						if (FileRowView(child).fileInfo.path == fileInfo.path) return FileRowView(child);
					}
				}
				return null;
			}
			
			public function getFileByFileInfo(fileInfo:FileInfo):File {
				var n:uint = fileInfos.length;
				for (var i:uint = 0; i < n; i++) {
					if (FileInfo(fileInfos.getItemAt(i)).path == fileInfo.path) {
						return files.getItemAt(i) as File;
					}
				}
				return null;
			}
			
			//----------------------
			// Error handling
			//----------------------
			
			public function handleError(message:String):void {
				messageNotifier.showMessage(message, true);
			}
			
		]]>
	</mx:Script>
	
	<mx:StringValidator id="peerIdValidator" minLength="64" maxLength="64" source="{hostIdInput}" property="text" triggerEvent=""/>
	<mx:DropShadowFilter id="dropShadowFilter" strength=".2" quality="3"/>
	
	<mx:Boolean id="youHaveFiles">{fileInfos.length > 0}</mx:Boolean>
	<mx:Boolean id="peerHasFiles">{peerClient.fileInfos.length > 0}</mx:Boolean>
	
	<mx:transitions>
		<mx:Transition fromState="*" toState="*">
			<mx:Fade target="{this}" duration="500" easingFunction="{Sine.easeOut}"/>
		</mx:Transition>
	</mx:transitions>
	
	<mx:states>
		<mx:State name="{CONNECTING_STATE}">
			<mx:AddChild position="before" relativeTo="{footer}">
				<mx:Text width="100%" text="Connecting to Stratus..." styleName="connectingMessage"/>
			</mx:AddChild>
			<mx:AddChild position="before" relativeTo="{footer}">
				<mx:Text width="100%" styleName="udpNote">
					<mx:htmlText><![CDATA[Note: Your network connection must allow for UDP traffic.]]></mx:htmlText>
				</mx:Text>
			</mx:AddChild>
		</mx:State>
		<mx:State name="{PEER_STATE}">
			<mx:AddChild position="before" relativeTo="{footer}">
				<mx:VBox width="100%" styleName="contentContainer" filters="{[dropShadowFilter]}" verticalGap="0">
					<mx:Text text="Instructions" styleName="sectionLabel"/>
					<mx:Text width="100%">
						<mx:htmlText><![CDATA[Give your host id to the person with whom you wish to connect.  Once connected, you may select the files that you would like to share.]]></mx:htmlText>
					</mx:Text>
					<mx:Spacer height="5"/>
					<mx:Text text="Your Host ID" styleName="sectionLabel"/>
					<mx:HBox horizontalGap="0" verticalAlign="middle">
						<mx:Text text="{hostId}" styleName="hostId"/>
						<mx:LinkButton styleName="copyButton" toolTip="Copy your host id to the clipboard." click="System.setClipboard(hostId);"/>
					</mx:HBox>
					<mx:Spacer height="5"/>
					<mx:Text text="Their Host ID" styleName="sectionLabel"/>
					<mx:TextInput id="hostIdInput" width="520"  styleName="hostId"/>
					<mx:Spacer height="5"/>
					<mx:Button label="Connect to peer" click="connectToPeer();"/>
				</mx:VBox>
			</mx:AddChild>
		</mx:State>
		<mx:State name="{FILES_STATE}">
			<mx:AddChild position="before" relativeTo="{footer}">
				<mx:VBox width="100%" styleName="contentContainer" filters="{[dropShadowFilter]}">
					<mx:Text text="Your files" styleName="sectionLabel"/>
					<mx:VBox styleName="sectionContainer" width="100%">
						<mx:Text width="100%" visible="{!youHaveFiles}" includeInLayout="{!youHaveFiles}">
							<mx:htmlText><![CDATA[Add files to be shared using the 'Add files' button below.]]></mx:htmlText>
						</mx:Text>
						
						<mx:Grid id="yourFilesGrid" width="100%" visible="{youHaveFiles}" includeInLayout="{youHaveFiles}">
							<mx:GridRow width="100%">
								<mx:GridItem width="40%">
									<mx:Text text="Name" styleName="columnHeader"/>
								</mx:GridItem>
								<mx:GridItem width="20%">
									<mx:Text text="Size" styleName="columnHeader"/>
								</mx:GridItem>
								<mx:GridItem>
									<mx:Text text="Progress" styleName="columnHeader"/>
								</mx:GridItem>
							</mx:GridRow>
							<mx:Repeater id="filesRpt" dataProvider="{fileInfos}" recycleChildren="false">
								<belugafile:FileRowView fileInfo="{FileInfo(filesRpt.currentItem)}" isYou="true" width="100%" clear="removeFileHandler(event);" cancel="cancelUpload();"/>
							</mx:Repeater>
						</mx:Grid>
						<mx:Button styleName="addButton" label="Add files" click="selectFiles();"/>
					</mx:VBox>
					
					<mx:Text text="Their files" styleName="sectionLabel"/>
					<mx:VBox styleName="sectionContainer" width="100%">
						<mx:Text width="100%" visible="{!peerHasFiles}" includeInLayout="{!peerHasFiles}">
							<mx:htmlText><![CDATA[They have not added any files yet.]]></mx:htmlText>
						</mx:Text>
						
						<mx:Grid id="theirFilesGrid" width="100%" visible="{peerHasFiles}" includeInLayout="{peerHasFiles}">
							<mx:GridRow width="100%">
								<mx:GridItem width="40%">
									<mx:Text text="Name" styleName="columnHeader"/>
								</mx:GridItem>
								<mx:GridItem width="20%">
									<mx:Text text="Size" styleName="columnHeader"/>
								</mx:GridItem>
								<mx:GridItem>
									<mx:Text text="Progress" styleName="columnHeader"/>
								</mx:GridItem>
							</mx:GridRow>
							<mx:Repeater id="peerFilesRpt" dataProvider="{peerClient.fileInfos}" recycleChildren="false">
								<belugafile:FileRowView fileInfo="{FileInfo(peerFilesRpt.currentItem)}" isYou="false" width="100%" open="downloadFileHandler(event);" cancel="cancelDownload();"/>
							</mx:Repeater>
						</mx:Grid>
					</mx:VBox>
				</mx:VBox>
			</mx:AddChild>
		</mx:State>
		<mx:State name="{ERROR_STATE}">
			<mx:AddChild>
				<mx:VBox width="100%" styleName="contentContainer">
					<mx:Text width="100%" text="{errorString}"/>
				</mx:VBox>
				
			</mx:AddChild>
		</mx:State>
	</mx:states>
	
	<mx:VBox id="footer" horizontalAlign="center" width="100%" verticalGap="2">
		<nbilyk:CSSImage styleName="logo"/>
		<mx:Text styleName="footerText" width="100%">
			<mx:htmlText>
				<![CDATA[Â© 2009 Nicholas Bilyk<br><a href="http://www.belugafile.com"><font color="#000099">www.belugafile.com</font></a>]]>
			</mx:htmlText>
		</mx:Text>
	</mx:VBox>
</mx:VBox>
